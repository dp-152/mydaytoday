apply plugin: 'com.android.library'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27
        buildToolsVersion = '27.0.3'

        versionCode 0
        versionName "0.1"
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:support-annotations:27.1.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // Test helpers for LiveData
    testImplementation "android.arch.core:core-testing:1.1.1"
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        title "Material CalendarView ${version}"
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompiler.source
            doFirst {
                classpath = files(variant.javaCompiler.classpath.files, project.android.getBootClasspath())
            }
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
        exclude 'android/support/v4/view/BetterViewPager.java'
        options {
            links "http://docs.oracle.com/javase/7/docs/api/"
            linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"
        }
    }
}


//apply from: 'https://raw.github.com/chrisbanes/gradle-mvn-push/master/gradle-mvn-push.gradle'
